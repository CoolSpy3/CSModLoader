plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group = 'com.coolspy3'

mainClassName = 'com.coolspy3.csmodloader.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

run {
    enableAssertions = true
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'ch.qos.logback:logback-core:1.2.7'
    implementation 'ch.qos.logback:logback-classic:1.2.7'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    testImplementation     'junit:junit:4.13'
}

run.dependsOn('build')

jar {
    manifest {
        attributes 'Main-Class': 'com.coolspy3.csmodloader.Main'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

javadoc {
    options {
        links 'https://docs.oracle.com/javase/8/docs/api/'
    }
    if (JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        options.addStringOption("-release", "8")
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri "https://maven.pkg.github.com/coolspy3/csmodloader"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'com.coolspy3'
            artifactId = 'csmodloader'
            version = '1.2.2'

            from components.java
        }
    }
}
